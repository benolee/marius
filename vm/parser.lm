
%include {
#include "parser_state.hpp"
#include "parser_tokens.h"
#include <iostream>
}

%name mariusParser

%token_type   { marius::ParserToken }
%default_type { marius::ParserToken }

%token_prefix TK_

%left OP0 CAST NEQUAL.
%left OP1.
%left OP2.
%left NOT.
%left DOT DCOLON.

%extra_argument { marius::ParserState* S }

%syntax_error {
  S->set_syntax_error();

  std::cout << "Syntax error at line " << S->line()
            << ", column " << S->column() << std::endl;
}

program ::= opt_fin l1(A) last_fin. { S->set_top(S->ret(A.a)); }

l1(A) ::= l1(B) fin l1e(C). { A.a = S->seq(B.a, C.a); }
l1(A) ::= l1e(B).    { A.a = B.a; }

l1e(A) ::= class(B). { A.a = B.a; }
l1e(A) ::= def(B).   { A.a = B.a; }
l1e(A) ::= stmt(B).  { A.a = B.a; }

fin_m ::= NL.
fin_m ::= SEMI.

opt_fin ::= .
opt_fin ::= opt_fin fin_m.

last_fin ::= EOF.
last_fin ::= fin EOF.

fin ::= fin fin_m.
fin ::= fin_m.

comma ::= COMMA NL.
comma ::= COMMA.

seq(A) ::= seq(B) fin stmt(C). { A.a = S->seq(B.a, C.a); }
seq(A) ::= stmt(B). { A.a = B.a; }

class_start ::= CLASS. { S->start_class(); }
class_body(A) ::= l1(B) fin END. { A.a = B.a; }

class(A) ::= class_start ID(B) fin class_body(C). {
               A.a = S->ast_class(B.s, 0, C.a);
             }

class(A) ::= class_start ID(B) LT s2(C) fin class_body(D). {
               A.a = S->ast_class(B.s, C.a, D.a);
             }

def_start ::= DEF. { S->start_def(); }
def_body(A) ::= seq(B) fin END. { A.a = B.a; }

arg ::= ID(A). { S->def_arg(A.s); }
arg ::= ID(A) CAST ID(B). { S->def_arg_cast(A.s, S->named(B.s)); }

id_list ::= id_list comma arg.
id_list ::= arg.

def_args ::= .
def_args ::= LP id_list RP.

def_name(A) ::= ID(B).  { A.s = B.s; }
def_name(A) ::= OP0(B). { A.s = B.s; }
def_name(A) ::= OP1(B). { A.s = B.s; }


def(A) ::= def_start def_name(B) def_args fin def_body(C). {
              A.a = S->ast_def(B.s, C.a);
            }
def(A) ::= def_start def_name(B) def_args fin END. {
              A.a = S->ast_def(B.s, S->ast_nil());
            }

lit(A) ::= NUM(B).                   { A.a = S->number(B.i); }
lit(A) ::= NIL.                      { A.a = S->ast_nil();  }
lit(A) ::= TRUE.                     { A.a = S->ast_true();  }
lit(A) ::= FALSE.                    { A.a = S->ast_false(); }
lit(A) ::= SELF.                     { A.a = S->self(); }
lit(A) ::= ID(B).                    { A.a = S->named(B.s); }
lit(A) ::= IVAR(B).                  { A.a = S->ivar_read(B.s); }
lit(A) ::= LITSTR(B).                { A.a = S->lit_str(B.s); }

np_arg(A) ::= lit(B). { A.a = B.a; }
np_arg(A) ::= np_arg(B) OP0(O) np_arg(C).
                { A.a = S->ast_binop(O.s, B.a, C.a); }
np_arg(A) ::= np_arg(B) OP1(O) np_arg(C).
                { A.a = S->ast_binop(O.s, B.a, C.a); }
np_arg(A) ::= np_arg(B) OP2(O) np_arg(C).
                { A.a = S->ast_binop(O.cs, B.a, C.a); }
np_arg(A) ::= np_arg(B) CAST np_arg(C).
                { A.a = S->cast(B.a, C.a); }
np_arg(A) ::= np_arg(B) NEQUAL np_arg(C).
                { A.a = S->not_(S->ast_binop("==", B.a, C.a)); }
np_arg(A) ::= np_arg(B) DOT ID(C). { A.a = S->call(B.a, C.s); }
np_arg(A) ::= np_arg(B) DOT ID(C) LP RP. { A.a = S->call(B.a, C.s); }
np_arg(A) ::= np_arg(B) DOT ID(C) LP np_arg_list RP.
                { A.a = S->call_args(B.a, C.s); }
np_arg(A) ::= np_arg(B) DCOLON(C) ID(D). { A.a = S->dcolon(B.a, C.s, D.s); }
np_arg(A) ::= np_arg(B) DOT DOLLAR lit(C).
                 { A.a = S->send_indirect(B.a, C.a); }
np_arg(A) ::= np_arg(B) DOT DOLLAR lit(C) LP RP.
                 { A.a = S->send_indirect(B.a, C.a); }
np_arg(A) ::= np_arg(B) DOT DOLLAR lit(C) LP np_arg_list RP.
                 { A.a = S->send_indirect_args(B.a, C.a); }
np_arg(A) ::= NOT np_arg(B). { A.a = S->not_(B.a); }
np_arg(A) ::= lambda(B). { A.a = B.a; }

np_arg_list_start ::= . { S->start_arg_list(); }
np_arg_list_elems ::= np_arg_list_elems comma np_arg(A). { S->add_arg(A.a); }
np_arg_list_elems ::= np_arg(A). { S->add_arg(A.a); }

np_kw_arg ::= ID(A) EQUAL np_arg(B). { S->add_kw_arg(A.s, B.a); }

np_kw_arg_list_elems ::= np_kw_arg_list_elems comma np_kw_arg.
np_kw_arg_list_elems ::= np_kw_arg.

np_opt_kw_arg_list ::= .
np_opt_kw_arg_list ::= comma np_kw_arg_list_elems.

np_arg_list ::= np_arg_list_start np_arg_list_elems np_opt_kw_arg_list.
np_arg_list ::= np_arg_list_start np_kw_arg_list_elems.

np_recv(A) ::= lit(B). { A.a = B.a; }
np_recv(A) ::= ID(C) LP RP. { A.a = S->call(S->self(), C.s); }
np_recv(A) ::= ID(C) LP np_arg_list RP. { A.a = S->call_args(S->self(), C.s); }
np_recv(A) ::= np_recv(B) DOT ID(C). { A.a = S->call(B.a, C.s); }
np_recv(A) ::= np_recv(B) DOT ID(C) LP RP. { A.a = S->call(B.a, C.s); }
np_recv(A) ::= np_recv(B) DOT ID(C) LP np_arg_list RP. { A.a = S->call_args(B.a, C.s); }
np_recv(A) ::= np_recv(B) DCOLON(C) ID(D). { A.a = S->dcolon(B.a, C.s, D.s); }
np_recv(A) ::= np_recv(B) DOT DOLLAR lit(C).
                 { A.a = S->send_indirect(B.a, C.a); }
np_recv(A) ::= np_recv(B) DOT DOLLAR lit(C) LP RP.
                 { A.a = S->send_indirect(B.a, C.a); }
np_recv(A) ::= np_recv(B) DOT DOLLAR lit(C) LP np_arg_list RP.
                 { A.a = S->send_indirect_args(B.a, C.a); }
np_recv(A) ::= lambda(B). { A.a = B.a; }
np_recv(A) ::= TUPLE_START np_arg_list RP. { A.a = S->tuple(); }
np_recv(A) ::= DICT_START np_arg_list RB. { A.a = S->dict(); }

s2(A) ::= np_recv(B) DOT ID(C) np_arg_list. { A.a = S->call_args(B.a, C.s); }
s2(A) ::= np_recv(B) DOT DOLLAR lit(C) np_arg_list.
            { A.a = S->send_indirect_args(B.a, C.a); }
s2(A) ::= ID(C) np_arg_list. { A.a = S->call_args(S->self(), C.s); }
s2(A) ::= np_recv(B) CAST np_arg(C).   { A.a = S->cast(B.a, C.a); }
s2(A) ::= np_recv(B) OP0(O) np_arg(C). { A.a = S->ast_binop(O.s, B.a, C.a); }
s2(A) ::= np_recv(B) OP1(O) np_arg(C). { A.a = S->ast_binop(O.s, B.a, C.a); }
s2(A) ::= np_recv(B) OP2(O) np_arg(C). { A.a = S->ast_binop(O.cs, B.a, C.a); }
s2(A) ::= np_recv(B) NEQUAL np_arg(C).
                { A.a = S->not_(S->ast_binop("==", B.a, C.a)); }
s2(A) ::= np_recv(B). { A.a = B.a; }
s2(A) ::= NOT s2(B). { A.a = S->not_(B.a); }

stmt(A) ::= s2(B). { A.a = B.a; }

/* Lambda */

lambda_start ::= LB. { S->start_lambda(); }

lambda_args ::= PIPE ID(B) PIPE. { S->def_arg(B.s); }
lambda_args ::= .

lambda(A) ::= lambda_start lambda_args seq(B) RB. { A.a = S->lambda(B.a); }

/* Conditions */

stmt(A) ::= IF s2(B) fin seq(C) fin END. { A.a = S->if_cond(B.a, C.a); }
stmt(A) ::= UNLESS s2(B) fin seq(C) fin END. { A.a = S->unless(B.a, C.a); }
stmt(A) ::= WHILE s2(B) fin seq(C) fin END. { A.a = S->while_(B.a, C.a); }

/* Import */

stmt(A) ::= IMPORT ID(B). { A.a = S->import(B.s); }

stmt(A) ::= TRY fin seq(C) fin RESCUE fin seq(D) fin END. {
              A.a = S->ast_try(C.a, D.a);
            }

stmt(A) ::= ID(B) EQUAL s2(C). { A.a = S->assign(B.s, C.a); }
stmt(A) ::= ID(B) OP1_SET(D) s2(C). { A.a = S->assign_op(B.s, D.s, C.a); }
stmt(A) ::= ID(B) OP0_SET(D) s2(C). { A.a = S->assign_op(B.s, D.s, C.a); }
stmt(A) ::= IVAR(B) EQUAL s2(C). { A.a = S->ivar_assign(B.s, C.a); }
stmt(A) ::= IVAR(B) OP1_SET(D) s2(C).
              { A.a = S->ivar_assign_op(B.s, D.s, C.a); }
stmt(A) ::= IVAR(B) OP0_SET(D) s2(C).
              { A.a = S->ivar_assign_op(B.s, D.s, C.a); }
stmt(A) ::= RAISE s2(B). { A.a = S->raise(B.a); }
